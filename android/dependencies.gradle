ext.appVersion = [
    "major": 1,
    "minor": 2,
    "patch": 1,
    "kind" : Kind.DEVELOPMENT
]

ext.builds = [
    buildTools: '28.0.3',
    minSdk    : 21,
    compileSdk: 28
]

ext.gradlePlugins = [
    kotlin   : '1.3.10',
    firebase : '1.1.1',
    gradle   : '3.2.1'
]

enum Kind {

    DEVELOPMENT(0, "-dev"),
    REGRESSION(1, "-reg"),
    PRODUCTION(2, "")

    private final int multiplier
    private final String suffix

    Kind(int multiplier, String suffix) {
        this.multiplier = multiplier
        this.suffix = suffix
    }

    int getAdditionValue() {
        this.multiplier * 100
    }

    String getSuffix() {
        this.suffix
    }
}

ext.generateVersionName = {
    return "${appVersion.major}.${appVersion.minor}.${appVersion.patch}${appVersion.kind.getSuffix()}"
}

ext.generateVersionCode = {
    return appVersion.major * 1000000 + appVersion.minor * 1000 + appVersion.patch + appVersion.kind.getAdditionValue()
}
